/**
 * Top-level build file for ftc_app project.
 *
 * It is extraordinarily rare that you will ever need to edit this file.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        // Note for FTC Teams: Do not modify this yourself.
        classpath 'com.android.tools.build:gradle:7.2.0'
    }
}


// This is now required because aapt2 has to be downloaded from the
// google() repository beginning with version 3.2 of the Android Gradle Plugin
allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url = "https://dl.bintray.com/first-tech-challenge/ftcsdk/" }
        maven { url 'https://jitpack.io' }
        maven { url = 'https://maven.brott.dev/' }

    }
}

repositories {
    mavenCentral()
}

dependencies {
    doc 'org.firstinspires.ftc:Hardware:9.0.1'
    doc 'org.firstinspires.ftc:RobotCore:9.0.1'
    doc 'org.firstinspires.ftc:FtcCommon:9.0.1'
    doc 'org.firstinspires.ftc:OnBotJava:9.0.1'
    doc 'org.firstinspires.ftc:Inspection:9.0.1'
}

task extractJavadoc {
    doLast {
        def componentIds = configurations.doc.incoming.resolutionResult.allDependencies.collect { it.selected.id }

        def result = dependencies.createArtifactResolutionQuery()
                .forComponents(componentIds)
                .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
                .execute()

        for (component in result.resolvedComponents) {
            component.getArtifacts(JavadocArtifact).each { artifact ->
                def version = artifact.identifier.componentIdentifier.version
                def libName = artifact.identifier.componentIdentifier.moduleIdentifier.name
                copy {
                    from zipTree(artifact.file)
                    into "docs/$version/$libName/"
                }
            }
        }
    }
}

